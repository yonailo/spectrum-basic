  10  REM Araignee
  20 LET rec=0: GO SUB 9000
  30 GO SUB 8000
  40 PRINT AT 0,0;"Score ";sc
  50 PRINT AT v,0;"  "
  60 LET v=v+(INKEY$="6"AND v<21)-(INKEY$="7"AND v>2)
  70 IF SCREEN$(v,2)<>" " THEN  GO SUB 1000
  80 PRINT AT v-1,0;"í"
  90 PRINT AT v,0; INK 6;"êë"
 100 FOR a=1 TO 6
 110 LET f(a)=f(a)-INT (RND*2): IF f(a)<1 THEN  PRINT AT a*3,1;"  ": LET f(a)=28
 120 PRINT AT a*3,f(a); INK 4;"ìî "
 130 NEXT a
 140 PRINT AT 0,19;"Temps ";INT temps;" ": LET temps=temps-.5: IF temps<0 THEN  GO TO 2000
 150 GO TO 50
 990 STOP 
1000 LET sc=sc+2
1010 PRINT AT v,2;"  "
1020 LET f(v/3)=28
1030 PRINT AT 0,0; INK 7;"Score ";sc
1040 RETURN 
2000  PRINT AT 5,9;"TERMINE!"
2010 PRINT ''TAB 6;"Score : ";sc
2020 IF sc>rec THEN  LET rec=sc
2030 PRINT ''"   Record : ";rec
2040 INPUT "Tapez "; FLASH 1;"ENTER"; FLASH 0;" pour rejouer "; LINE a$: GO TO 30
8000 BORDER 1: PAPER 1: INK 7: CLS 
8010 LET v=10
8020 DIM f(6)
8030 FOR a=1 TO 6: LET f(a)=28: NEXT a
8040 LET sc=0
8050 RANDOMIZE 
8060 FOR a=1 TO v-1: PRINT AT a,0;"í": NEXT a
8070 PLOT 0,168: DRAW 255,0
8080 LET temps=99
8990 RETURN 
9000 FOR a=USR "a" TO USR "e"+7
9010 READ user: POKE a,user
9020 NEXT a: RETURN 
9030 DATA 0,17,63,94,111,23,45,0
9040 DATA 0,108,248,220,252,248,96,0
9050 DATA 16,16,16,16,16,16,16,16
9060 DATA 0,17,63,94,111,23,45,0
9070 DATA 0,252,2,2,252,224,0,0
9080 REM êë = ab
9090 REM í  = c
9100 REM ìî = de
